import java.util.concurrent.ConcurrentHashMap

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
    id 'eclipse'
    id 'idea'
}

sourceCompatibility = targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'by.radioegor146.Main'
}

if (!hasProperty('ide.eclipse')) {
    sourceSets.test.resources.srcDirs += file('test_data')
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation project(':annotations')

    implementation 'org.ow2.asm:asm:9.5'
    implementation 'org.ow2.asm:asm-tree:9.5'
    implementation 'org.ow2.asm:asm-commons:9.5'

    implementation 'info.picocli:picocli:4.6.3'

    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.apache.logging.log4j:log4j-core:2.12.4'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.12.4'
    implementation 'org.apache.logging.log4j:log4j-api:2.12.4'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

shadowJar {
    classifier null
}

assemble.dependsOn(shadowJar)

jar {
    manifest.attributes("Main-Class": getProperty('mainClass'))
}

ConcurrentHashMap<String, StringBuilder> testOutputs = new ConcurrentHashMap<>();

test {
    useJUnitPlatform()

    maxParallelForks = Math.max(Runtime.runtime.availableProcessors(), 32)

    testLogging {
        events = []
    }

    beforeTest { descriptor ->
        if (System.getenv("CI"))
            System.out.print("\u001b[1;33m");
        testOutputs.put(descriptor.getName(), new StringBuilder());
        System.out.print("Running test \"" + descriptor.getDisplayName() + "\" -> ");
        System.out.flush();
    }

    afterTest { descriptor, result ->
        if (System.getenv("CI")) {
            if (result.getResultType() == TestResult.ResultType.SUCCESS)
                System.out.print("\u001b[1;32m");
            if (result.getResultType() == TestResult.ResultType.FAILURE)
                System.out.print("\u001b[1;31m");
            if (result.getResultType() == TestResult.ResultType.SKIPPED)
                System.out.print("\u001b[1;34m");
        }
        System.out.print(result.getResultType().toString());
        if (System.getenv("CI"))
            System.out.print("\u001b[1;33m");
        System.out.println(" in " + ((result.getEndTime()) - (result.getStartTime())) + "ms");
        if (result.getResultType() == TestResult.ResultType.FAILURE) {
            if (System.getenv("CI")) {
                // System.out.println("travis_fold:start:fail_log");
                System.out.print("\u001b[1;31m");
            }
            System.out.println("Test fail log");
            System.out.print(testOutputs.get(descriptor.getName()).toString());
            testOutputs.remove(descriptor.getName());
            if (System.getenv("CI")) {
                // System.out.println("travis_fold:end:fail_log");
            }
        }
    }

    onOutput { descriptor, event ->
        if (!testOutputs.containsKey(descriptor.getName()))
            testOutputs.put(descriptor.getName(), new StringBuilder());
        testOutputs.get(descriptor.getName()).append("[")
                .append(event.getDestination().toString().toUpperCase()).append("] ").append(event.getMessage());
    }
}

if (hasProperty('ide.eclipse'))
    processTestResources {
        from 'test_data'
    }